\
\ The following algorithm is taken from the css-color-3 w3c spec
\ https://www.w3.org/TR/css-color-3/#hsl-color
\

HOW TO RETURN hsl.to.rgb(h, s, l):
    SELECT:
        l<=0.5: PUT l*(s+1) IN m2
        ELSE: PUT l+s-l*s IN m2
    PUT l*2-m2 IN m1
    PUT hue.to.rgb(m1, m2, h+1/3) IN r
    PUT hue.to.rgb(m1, m2, h    ) IN g
    PUT hue.to.rgb(m1, m2, h-1/3) IN b
    RETURN (r, g, b)

HOW TO RETURN hue.to.rgb(m1, m2, h):
    IF h<0: PUT h+1 IN h
    IF h>1: PUT h-1 IN h
    IF h*6<1: RETURN m1+(m2-m1)*h*6
    IF h*2<1: RETURN m2
    IF h*3<2: RETURN m1+(m2-m1)*(2/3-h)*6
    RETURN m1


\
\ The following is a custom script to iterate HSL values and write out JSON
\ of the corresponding RGB value accoring to the above algorithm:
\

WRITE "module CssHslReferenceData exposing (json)" / / /
WRITE "json : String" /
WRITE "json =" /
WRITE "    """""""
WRITE "["
PUT 0 IN is.first
FOR hi IN {0..36}:
    PUT hi/36 IN h
    FOR si IN {0..10}:
        PUT si/10 IN s
        FOR li IN {0..10}:
            PUT li/10 IN l
            PUT hsl.to.rgb(h, s, l) IN r, g, b
            IF is.first > 0:
                WRITE / ","
            WRITE "{""r"":"
            WRITE r
            WRITE ",""g"":"
            WRITE g
            WRITE ",""b"":"
            WRITE b
            WRITE ",""h"":"
            WRITE h
            WRITE ",""s"":"
            WRITE s
            WRITE ",""l"":"
            WRITE l
            WRITE "}"
            PUT 1 IN is.first
WRITE / "]"
WRITE """""""" /
